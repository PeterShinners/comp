// Comp Language - Common Grammar Rules
// Shared between scan.lark and comp.lark
//
// Contains: comments, strings, numbers, identifiers
// These are the fundamental building blocks that both grammars need.

// === COMMENTS ===

// Doc comments: --- content ---
DOC_OPEN.5: "---"
DOC_CONTENT.5: /(.|\n)+?(?=---)/
_doc_comment: DOC_OPEN DOC_CONTENT? DOC_OPEN
doc_comment: _doc_comment

// Line comments: -- content
LINE_OPEN.5: "--"
LINE_CONTENT.5: /[^\r\n]+/
_line_comment: LINE_OPEN LINE_CONTENT?
line_comment: _line_comment


// === STRING LITERALS ===

// String parsing with separated quotes and content.
// Short strings: "content" (no newlines allowed)
// Long strings: """content""" (newlines allowed)
DUBQUOTE.3: "\""
SIXQUOTE.3:  "\"\"\""
SHORT_TEXT_CONTENT.3: /(?:[^"\\\r\n]|\\.)+/
_short_text: DUBQUOTE SHORT_TEXT_CONTENT? DUBQUOTE
LONG_TEXT_CONTENT.3: /(.|\n)+?(?=""")/s
_long_text: SIXQUOTE LONG_TEXT_CONTENT? SIXQUOTE
text: _short_text | _long_text


// === NUMBER LITERALS ===

// Identifies integer only values with bases, which need different parsing
// route. Allow underscores for readability.
// Leading signs are handled by unary operators

number: INTBASE | DECIMAL

// Based number formats - unsigned, signs handled by unary operators
// Decimal formats - unsigned, signs handled by unary operators
INTBASE.2: /0[xXbBoO](_?[\da-zA-Z])+/                      // All bases: hex, binary, octal
DECIMAL.1: /(([1-9](_?\d)*)|0(_?0)*)/                      // Integer: 123, 1_000, 0, 0_0
         | /\d(_?\d)*\.(\d(_?\d)*)?(SCIENTIFIC)?/  // Decimal: 3.14, 5., 1.5e-10, 5.e10
         | /\.\d(_?\d)*(SCIENTIFIC)?/              // Leading dot: .5, .123_456, .5e10
         | /\d(_?\d)*SCIENTIFIC/                   // Scientific integer: 1e5, 1_000e10
SCIENTIFIC.1: /[eE][+-]?\d(_?\d)*/

// === IDENTIFIER TOKENS ===

// Basic identifier token - allows kebab-case
TOKENFIELD.4: /[a-zA-Z_][\w-]*/

// Dot for field access
DOT.6: "."


// === WHITESPACE ===
// Each importing grammar should add their own %ignore WS

WS: /\s+/
