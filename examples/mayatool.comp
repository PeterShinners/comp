-- Two examples of working with Maya

import.maya = ("maya-comp-spectacular@1.0.3" comp)


--- Maya tool to create offset transforms
First is an example of inserting a parent between two nodes and
positioning it partway between them. This relies on the parenting
commands that preserve positioning when reparenting nodes.
(which famously does not work once things are animated)

Based on https://github.com/TrevisanGMW/gt-tools
---

layer-config = ~(
    suffix~text
    parent-type~(parent | selection)
    layer-type~(joint | locator | group)
    outliner-color~(~num ~num ~num) = (1.0 0.5 0.0) -- orange
)

create-inbetween = :arg~layer-config (|val
    !transact(maya.scene maya.preserve-selection)

    maya.selection ()
    |map :obj (
        var.parent = obj |maya.get-parent()

        var.transform = var.parent
        |maya.create-node(arg.layer-type name=format("$(obj.name)$(arg.suffix)"))
        |maya.set-attr(useOutlinerColor true)
        |maya.set-attr(outlinerColor arg.outliner-color)
        |maya.parent(var.parent)

        var.target = if(arg.parent-type == parent):(var.parent) |else:(obj)
        if(var.target |maya.exists()) : (
            var.target |maya.constrain(var.transform)
        )

        obj |maya.parent(var.transform)
    )
)


--- Create random bubble shapes
Create a group of random positioned and sized spheres around the selected
objects.

Based on https://github.com/nestrada2/bubbles
---

bubble-args = ~(
    count~num = 10
    radius-range~(~num ~num) = (0.1 0.5)
    position-range~(~num ~num) = (0.1 5.0)
)

create-bubbles = :parent arg~bubble-args (|val
    !transact(maya.scene)

    var.group = parent 
    |maya.create-node(group name="bubbles") 
    |maya.select(replace)

    !transact(maya.preserve-selection)

    range(arg.count) |map :i (
        var.position = repeat(3) : (random(arg.position-range))
        var.group
        |maya.poly-sphere(name=format("bubble-$(i)") radius=random(arg.radius-range))
        |maya.xform(position=var.position)
    )
)
