/** 
Shopping cart module
Define several simplistic shapes and modification functions to manage
the contents of a shopping cart.

Highlights:

- At build time this entire example is compiled into a single print
statement with a constant static string.

*/

!shape item ~{name~text price~num quantity~num=1}
!shape cart ~{items~item{} discount~num=0}

!func main (
    {
        {"Apple" price=1.00}
        {"Banana" price=0.50 quantity=2}
        {"Apple" 1.00 quantity=2}  // replaces previous apples
    }
    | map[item]  // morph each simple structure into a full cart item
    | reduce (upsert ($name))
    | cart[discount=0.2]
    | total
    | print["Total: %()"]
)

!pure total ~cart ( 
    $items | sum ($price * $quantity) * (1 - $discount)
)

/// Insert or update an item in a collection
!pure upsert ~collection (
    !mods item~item
    !block key ~item=pass

    $ | replace[item limit=1] (key) | else (append[item])
)
