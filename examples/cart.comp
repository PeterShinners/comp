mod.doc: """Simple shopping cart example

Gives some ideas of working with an updating state in
an functional, immutable system.
"""

shopping-cart: {
    items: cart-item[] @default:()
    discount: num @default:0
}


cart-item: {
    name: str
    price: num
    quantity: num
}


!main [
    ; Sample items to add to cart
    (
        (name:Apple price:1.00 quantity:2)
        (Banana 0.50 3)  ; names optional if field ordering matches shape
        (name:Apple price:1.00 quantity:1)  ; Will replace first apple
    )

	; Individually add item definitions to cart
    -> reduce(
		initial: ~shopping-cart
        ~(
            {cart:.in.accum  item:.in.value}
            -> add-item()
		)
    )

    ; Apply 20% discount
    -> set-discount(0.2)

    ; Calculate and display total (expected 2.00, precise, no math errors)
    -> cart-total() $ "Total: ${}"
    -> print()
]


add-item: [
	*: .in				; Fill with existing values
	items: .in.items	; Insert or update name match
		-> upsert(.arg [.in.name == .arg.name])
]
@call: shopping-cart
@arg: cart-item
@doc: "Add or replace item in cart by name"


cart-total: [
    *:
	.in.items
		-> sum([.in.price * .in.quantity])
		* (1 - .in.discount)
]
@call: shopping-cart
@doc: "Calculate total price with discount applied"


set-discount: [
	*: .in   ; Start with original cart data
	*: .arg  ; Override with argument fields (discount)
]
@call: shopping-cart
@arg: {discount: num}
@doc: "Set discount amount (0.2 = 20% off)"

