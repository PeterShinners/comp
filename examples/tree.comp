--- Binary search tree example
Trivial sketches of building, modifying, and querying binary trees.
---

tree = ~(
	value~num
	left~branch = nil
	right~branch = nil
)

branch = ~(tree | nil)


main = :(
	-- Build tree from values
	var.tree = (5 3 8 1 7 9) 
	|reduce (initial=tree() : it(
		it.accum |tree-insert(it.val)
	))

	-- Test finding values
	var.seven = tree |tree-contains(7)  -- true
	var.four = tree |tree-contains(4)  -- false

	-- Get sorted values
	tree-values(tree) |print()    -- [1 3 5 7 8 9]
)

-- Insert value into binary search tree

tree-insert = :tree~nil value~num pure (|val
	tree(value)
)

tree-insert = :tree~branch value~num pure (|mix
	tree 
	if(value < tree.value) 
		: (left = tree-insert(tree.left value))
	|else 
		: (right = tree-insert(tree.right value))
)

-- Search existance of value in tree

tree-contains = :tree~nil value~num pure (|val
	false
)

tree-contains = :tree~branch value~num pure (|val
	if(value < tree.value)
		:(tree-contains(tree.left value))
	|elif(value > tree.value)
		:(tree-contains(tree.right value))
	|else
		(true)
)

-- Generate sequence of sorted tree members

tree-values = :tree~nil pure (
)

tree-values = :tree~branch pure (|flat
	tree-values(tree.left) 
	tree.value 
	tree-values(tree.right)
)
